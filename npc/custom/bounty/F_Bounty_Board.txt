//===== Atlantis Script ======================================
//= iRO Bounty Boards
//===== By: ==================================================
//= Ragno / AtlantisRO
//===== Actual Version: ======================================
//= 1.0 Starting Release
//===== General Description: =================================
//= Hunting Quest designed by iRO:
//=    http://irowiki.org/wiki/Bounty_Board_Quests
//===== How to Use: ==========================================
//= Uses function "F_Bounty_Board" to manage the Bounty Boards
//= npc on cities.
//= The function arguments are this:
//= 	callfunc "F_Bounty_Board", MinLevel, MaxLevel, MsgHuntPlace, MsgOutLevel,
//=			MonsterID, 		QuestID, 	BaseExp, 	JobExp{, 
//=			MonsterID, 		QuestID, 	BaseExp, 	JobExp};
//============================================================

function	script	F_Bounty_Board	{
	if (BaseLevel < getarg(0) || (BaseLevel > getarg(1) && getarg(1))){
		for (.@i = 5; .@i < getargcount(); .@i += 4){
			if (questprogress(getarg(.@i))){
				mes "[Bounty Board]";
				mes "You are ready to collect your bounty.";
				mes "Which bounty do you want to collect?";
				next;
				switch(select("Collect 1 bounty", "I want to QUIT hunting", "cancel")){
					case 1: goto L_BountyOne;
					case 3: goto L_BountyDel;
					case 4: close;
				}
			}
		}
		switch(getarg(3)){
			case 1: mes "These bounties are available only to adventurers who are between the levels "+getarg(0)+"-"+getarg(1)+"."; break;
			case 2: mes "These bounties are available only to adventurers who are between levels "+getarg(0)+" and "+getarg(1)+"."; break;
			case 3: mes "These bounties are available only to adventurers who are above level "+getarg(0)+"."; break;
			case 4: mes "These bounties are available only to adventurers who are higher than level "+getarg(0)+"."; break;
			case 5: mes "You've stumbled upon what appears to be a bulletin board with wanted ads. Nothing looks that interesting."; break;
		}
		close;
	}
	mes "[Bounty Board]";
	mes "You've stumbled upon what appears to be a bulletin board with wanted ads.";
	mes "Take a closer look?";
	next;
	mes "[Bounty Board]";
	mes "There are many listings for monster hunting.";
	next;
	switch(select("Check bounty missions", "Collect bounty rewards", "Where do I go to hunt?", "Search for other bounty hunters.", "I want to QUIT hunting", "Not interested.")){
	case 1:	// Check bounty missions
		mes "[Bounty Board]";
		mes "There is a list of monsters on the board.";
		mes "Looking through it you decide to take one hunting task.";
		next;
		for (.@i = 4; .@i < getargcount(); .@i += 4)
			.@menu$ += getmonsterinfo(getarg(.@i),MOB_NAME) + ":";
		.@menu$ += "Hunt All:Cancel";
		.@bounty = (select(.@menu$) * 4) + 1;
		if (.@bounty > getargcount()+1){
			mes "[Bounty Board]";
			mes "You have decided not to take any bounties.";
			close;
		} else if (.@bounty == getargcount()+1){
			for (.@i = 5; .@i < getargcount(); .@i += 4){
				if (!questprogress(getarg(.@i)))
					setquest getarg(.@i);
				.@HuntAllList$ += (.@i+4 > getargcount()?" and ":(.@i > 5?", ":"")) + getmonsterinfo(getarg(.@i-1),MOB_NAME);
			}
			mes "[Bounty Board]";
			mes "There is a bounty to hunt 150 " + .@HuntAllList$ + " hunt them all.";
			close;
		}
		.@monster$ = getmonsterinfo(getarg(.@bounty-1),MOB_NAME);
		mes "[Bounty Board]";
		mes "There is a bounty to hunt 150 " + .@monster$ + ".";
		next;
		if (select("I'll do it.", "Uhh actually nevermind.") == 1){
			if (!questprogress(getarg(.@bounty),HUNTING))
				setquest getarg(.@bounty);
			mes "[Bounty Board]";
			mes "You have accepted the bounty to hunt "+.@monster$+".";
		}
		close;
	case 2:	// Collect bounty rewards
		mes "[Bounty Board]";
		mes "You are ready to collect your bounty.";
		mes "Which bounty do you want to collect?";
		next;
		switch(select("Collect 1 bounty", "cancel")){
			case 1: goto L_BountyOne;
			case 2: close;
		}
	case 3:	// Where do I go to hunt?
		mes "[Bounty Board]";
		switch(getarg(2)){
			case 1: mes "You can hunt them at the dungeon. You're staring at the entrance now!"; break;
			case 2: mes "You can hunt them at the forests surrounding Payon and at the Cave up north at Archer Village."; break;
			case 3: mes "You can hunt them at various fields surrounding Prontera."; break;
			case 4: mes "You can hunt them at various fields and dungeons around Prontera."; break;
			case 5: mes "You can hunt at the Umbala Fields to fulfill these bounties."; break;
			case 6:
				mes "Wich monster hunting field do you want to go to?";
				next;
				.@bounty = (select("Golem", "Stalactic Golem", "Medusa", "Cancel") * 4) + 1;
				if (.@bounty > getargcount())
					close;
				mes "[Bounty Board]";
				if (!questprogress(getarg(.@bounty),HUNTING)){
					mes "You're currently not hunting "+getmonsterinfo(getarg(.@bounty-1),MOB_NAME)+".";
					close;
				}
				mes "Sending you now.";
				close2;
				switch(.@bounty){
					case 1: warp "cmd_fild06",147,36; break;
					case 2: warp "beach_dun2",154,25; break;
					case 3: warp "beach_dun",266,67; break;
				}
				end;
			case 7: mes "You can hunt at the <NAVI>Juno Fields<INFO>yuno,158,16,</INFO></NAVI> to fulfill these bounties."; break;
			case 8: mes "You can hunt at <NAVI>Brasilis Field<INFO>brasilis,309,304,</INFO></NAVI> and <NAVI>Brasilis Dungeon<INFO>brasilis,183,284,</INFO></NAVI> to fulfill these bounties."; break;
			case 9: mes "You can hunt them at various fields surrounding Lighthalzen."; break;
			case 10: mes "You can hunt them at various fields and dungeons around Lighthalzen."; break;
			case 11:
				mes "You can hunt at <NAVI>Splendide Field<INFO>mid_camp,14,143,</INFO></NAVI> to fulfill these bounties.";
				mes "Be sure that you're an authorized member of the Midgard Alliance before you go there.";
				break;
			case 12: mes "You can hunt at the two Dicastes Fields to fulfill these bounties."; break;
		}
		close;
	case 4:	// Search for other bounty hunters.
		mes "[Bounty Board]";
		mes "To find players to party with, click on the [Booking] button in, your user menu or type \"/booking\" in the chat box to bring up the window.";
		next;
		mes "[Bounty Board]";
		mes "Then click \"Search\" to bring up all party listings.";
		mes "To post a party wanted ad, type \"/recruit\" into the chat box.";
		close;
	case 5:	// I want to QUIT hunting
		goto L_BountyDel;
	case 6:	// Not interested.
		mes "[Bounty Board]";
		mes "Life is full of dangers.";
		mes "You don't live life unless you put yourself in danger from time to time";
		close;
	}

L_BountyOne:
	mes "[Bounty Board]";
	mes "You will receive zeny and EXP as reward";
	next;
	for (.@i = 4; .@i < getargcount(); .@i += 4)
		.@menu$ += getmonsterinfo(getarg(.@i),MOB_NAME) + ":";
	.@menu$ += "Cancel";
	.@bounty = (select(.@menu$) * 4) + 1;
	if (.@bounty > getargcount()){
		mes "^000000[Bounty Board]^000000";
		mes "Come back when you have a bounty completed.";
		close;
	}
	if (questprogress(getarg(.@bounty),HUNTING) == 2){
		.@BaseExp = getarg(.@bounty+1);
		.@JobExp = getarg(.@bounty+2);
		erasequest getarg(.@bounty);
		if (Zeny + 37500 <= MAX_ZENY)
			Zeny += 37500;
		getexp .@BaseExp,.@JobExp;
		//give cashpoints
		setd("#CASHPOINTS"), getd("#CASHPOINTS") + 10;
		getitem 7863,1;
		mes "[Bounty Board]";
		mes "Here is your reward.";
	} else {
		mes "^000000[Bounty Board]^000000";
		mes "The bounty to hunt "+getmonsterinfo(getarg(.@bounty-1),MOB_NAME)+" has not been completed.";
	}
	close;

L_BountyDel:
	mes "[Bounty Board]";
	mes "I'll check what missions that you've taken so far.";
	mes "Which ones do you want to quit?";
	next;
	for (.@i = 5; .@i < getargcount(); .@i += 4){
		if (questprogress(getarg(.@i),HUNTING) == 1){
			.@monster$ = getmonsterinfo(getarg(.@i-1),MOB_NAME);
			mes "[Bounty Board]";
			mes "You have NOT completed the " + .@monster$ + " hunt mission.";
			mes "Do you want to continue or quit?";
			next;
			if (select("Keep hunting "+.@monster$, "^FF0000Get rid of my quest^000000") == 2){
				erasequest getarg(.@i);
				mes "[Bounty Board]";
				mes "All of your records for this quest have been deleted.";
				close2;
			}
		}
	}
	close;

}
